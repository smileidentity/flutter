def kotlinVersion = findProperty('kotlinVersion') ?: '2.1.0'
//def smileVersion = findProperty('smileVersion') ?: '10.6.1-SNAPSHOT' // Uncomment this line to use the latest snapshot version also uncomment the snapshot repository
def smileVersion = findProperty('smileVersion') ?: '10.6.1-20250512.190207-63'
def kotlinCompilerExtension = findProperty('kotlinCompilerExtensionVersion') ?: '1.5.14'

ext {
    project.ext.kotlinVersion = kotlinVersion
    project.ext.smileVersion = smileVersion
    project.ext.kotlinCompilerExtension = kotlinCompilerExtension
}

buildscript {
    def kotlinVersion = rootProject.findProperty('kotlinVersion') ?: '2.1.0'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven {
            url 'https://central.sonatype.com/repository/maven-snapshots/'
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.7.3"
        if (kotlinVersion?.startsWith("2")) {
            classpath "org.jetbrains.kotlin:compose-compiler-gradle-plugin:$kotlinVersion"
        } else {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        }
        classpath "org.jlleitschuh.gradle:ktlint-gradle:12.1.2"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://central.sonatype.com/repository/maven-snapshots/'
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "org.jlleitschuh.gradle.ktlint"
if (kotlinVersion?.startsWith("2")) {
    apply plugin: "org.jetbrains.kotlin.plugin.compose"
}

android {
    namespace 'com.smileidentity.flutter'
    compileSdk 35

    defaultConfig {
        minSdk 21
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ['-Xskip-metadata-version-check'] // metadata version check skip flag
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    buildFeatures.compose = true
    if (!kotlinVersion?.startsWith("2")) {
        composeOptions {
            kotlinCompilerExtensionVersion = kotlinCompilerExtension
        }
    }

    dependencies {
        implementation "com.smileidentity:android-sdk:${smileVersion}"
        implementation "androidx.core:core-ktx"
        implementation "androidx.compose.ui:ui"
        implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
        implementation "androidx.compose.material3:material3"
        implementation "androidx.fragment:fragment-ktx"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core"
        implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.8"
        implementation "com.google.mlkit:object-detection:17.0.2"

        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "io.mockk:mockk:1.13.13"
    }
}

ktlint {
    android = true
    filter {
        exclude { it.file.path.contains(".g.kt") }
    }
}
group "com.smileidentity.flutter"
version findProperty("SDK_VERSION") ?: "10.4.0"

buildscript {
    ext.kotlin_version = "1.9.10"
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.7.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:12.1.1"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    namespace group
    compileSdk 35

    defaultConfig {
        minSdk 21
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    buildFeatures.compose = true
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.3"
    }

    dependencies {
        implementation "com.smileidentity:android-sdk:${version}"
        implementation "androidx.core:core-ktx"
        implementation "androidx.compose.ui:ui"
        implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
        implementation "androidx.compose.material3:material3"
        implementation "androidx.fragment:fragment-ktx"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core"
        implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.6"

        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "io.mockk:mockk:1.13.11"
    }
}

ktlint {
    android = true
    filter {
        exclude { it.file.path.contains(".g.kt") }
    }
}
